buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}
	dependencies {
		classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
		classpath "gradle.plugin.com.github.gradle-git-version-calculator:gradle-git-version-calculator:1.0.0"
	}
}

plugins {
	id 'io.codearte.nexus-staging' version '0.11.0'
	id 'nu.studer.credentials' version '1.0.4'
}

ext {
	publish_user = project.hasProperty('publish_user') ? project.getProperty('publish_user') : credentials.publish_user
	publish_pass = project.hasProperty('publish_pass') ? project.getProperty('publish_pass') : credentials.publish_pass
}

def generateBaseName(artifact_id) {
	return "paseto-" + artifact_id
}

nexusStaging {
	username = publish_user
	password = publish_pass
}

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'com.github.gradle-git-version-calculator'
	apply plugin: 'jacoco'


	gitVersionCalculator {
		prefix = 'release@'
		withSnapshot = true
	}

	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url  "https://dl.bintray.com/terl/lazysodium-maven"
		}
	}

	jar {
		version = gitVersionCalculator.calculateVersion()
	}

	if (it.name != "paseto" && it.name != "meta" && it.name != "threeten" && it.name != "test") {

		apply plugin: 'signing'
		apply plugin: 'maven-publish'

		afterEvaluate {
			signing {
				sign publishing.publications.maven
			}
		}

		publishing {
			publications {
				group = "net.aholbrook.paseto"
				version = gitVersionCalculator.calculateVersion()

				maven(MavenPublication) {
					pom {
						url = 'https://github.com/atholbro/paseto'

						licenses {
							license {
								name = 'The MIT License (MIT)'
								url = 'http://opensource.org/licenses/MIT'
							}
						}

						developers {
							developer {
								id = 'aholbrook'
								name = 'Andrew Holbrook'
								email = 'atholbro@gmail.com'
							}
						}

						scm {
							connection = 'scm:git:git@github.com:atholbro/paseto.git'
							developerConnection = 'scm:git:git@github.com:atholbro/paseto.git'
							url = 'https://github.com/atholbro/paseto'
						}


					}
				}
			}
			repositories {
				maven {
					def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
					def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
					url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

					credentials {
						username publish_user
						password publish_pass
					}
				}
			}
		}
	}
}

subprojects {
	sourceCompatibility = 1.8

	dependencies {
		testImplementation 'junit:junit:4.12'
	}

	jacocoTestReport {
		additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
		sourceDirectories = files(sourceSets.main.allSource.srcDirs)
		classDirectories =  files(sourceSets.main.output)
		reports {
			html.enabled = true
			xml.enabled = true
			csv.enabled = false
		}
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allJava
		classifier = 'sources'
	}

	task javadocJar(type: Jar) {
		from javadoc
		classifier = 'javadoc'
	}
}

task allDependencies {
	subprojects.each { dependsOn("${it.name}:dependencies") }
}

task jacocoRootReport(type: JacocoReport) {
	dependsOn = subprojects.test
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs).filter { File f -> !f.path.contains('threeten') }
	additionalSourceDirs = sourceDirectories
	classDirectories = files(subprojects.sourceSets.main.output).filter { File f -> !f.path.contains('threeten') }
	executionData = files(subprojects.jacocoTestReport.executionData).filter { File f -> !f.path.contains('threeten') }
	reports {
		html.enabled = true
		xml.enabled = true
		csv.enabled = false
	}
	onlyIf = {
		true
	}
	doFirst {
		executionData = files(executionData.findAll {
			it.exists()
		})
	}

	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, excludes: [
					'net/aholbrook/paseto/test/**',
					'net/aholbrook/paseto/encoding/json/jackson/mixin/**'
			])
		})
	}
}

check.dependsOn jacocoRootReport
